plugins {
    id "org.jetbrains.kotlin.jvm" version "1.3.72"
    id "com.github.johnrengelman.shadow" version "6.0.0"
    id "org.jmailen.kotlinter" version "1.26.0"
    id "io.gitlab.arturbosch.detekt" version "1.1.1"
}

group "net.wussmann"

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    def awsSdk_version = "1.11.822"
    def http4k_version = "3.243.0"
    def exposed_version = "0.24.1"

    implementation "org.jetbrains.kotlin:kotlin-stdlib"

    implementation "com.amazonaws:aws-lambda-java-core:1.2.1"
    implementation "com.amazonaws:aws-lambda-java-events:3.1.0"

    implementation "org.http4k:http4k-serverless-lambda:$http4k_version"
    implementation "org.http4k:http4k-core:$http4k_version"
    implementation "org.http4k:http4k-format-jackson:$http4k_version"
    implementation "org.http4k:http4k-contract:$http4k_version"
    implementation "org.http4k:http4k-server-undertow:$http4k_version"

    implementation "org.jetbrains.exposed:exposed-core:$exposed_version"
    implementation "org.jetbrains.exposed:exposed-dao:$exposed_version"
    implementation "org.jetbrains.exposed:exposed-jdbc:$exposed_version"
    implementation "org.postgresql:postgresql:42.2.14"
    implementation "com.zaxxer:HikariCP:3.4.5"

    implementation "com.amazonaws:aws-java-sdk-cloudwatch:$awsSdk_version"
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "11"
        freeCompilerArgs = ["-Werror", "-Xopt-in=kotlin.time.ExperimentalTime"]
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "11"
        freeCompilerArgs = ["-Werror", "-Xopt-in=kotlin.time.ExperimentalTime"]
    }
}

detekt {
    input = files("**/src/main/kotlin")
    filters = ".*/resources/.*,.*/build/.*,.*test.*,.*/model/.*"
    config = files("detekt-config.yml")
}

task install(type: Exec) {
    commandLine("yarn", "install")
}

task deploy(type: Exec) {
    dependsOn("shadowJar", "install")
    commandLine("npx", "sls", "deploy", "--stage", System.getenv("STAGE") ?: "dev")
}

task remove(type: Exec) {
    commandLine("npx", "sls", "remove", "--stage", System.getenv("STAGE") ?: "dev")
}
